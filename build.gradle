plugins {
    id 'java'
    id 'groovy'
    id 'application'

    // Code coverage of tests
    id 'jacoco'

    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "net.nemerosa.versioning" version "2.15.1"
    id "nebula.ospackage" version "9.1.1"
}

repositories {
    mavenCentral()
    mavenLocal()
}

group = projectGroup
version = projectVersion
sourceCompatibility = projectJavaVersion
targetCompatibility = projectJavaVersion

dependencies {
    annotationProcessor 'info.picocli:picocli-codegen:4.6.2'
    implementation 'info.picocli:picocli:4.6.1'
    implementation 'org.jsoup:jsoup:1.14.3'
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    implementation 'com.squareup.moshi:moshi:1.13.0'
    implementation 'com.serjltt.moshi:moshi-lazy-adapters:2.2'
    implementation 'org.tomlj:tomlj:1.0.0'
    implementation 'org.influxdb:influxdb-java:2.22'
    implementation 'org.slf4j:slf4j-api:1.7.35'
    implementation 'org.slf4j:slf4j-simple:1.7.35'

    testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.9.3'
    testImplementation 'org.slf4j:slf4j-simple:1.7.35'
}

application {
    mainClass.set('biz.nellemann.hmci.Application')
    applicationDefaultJvmArgs = [ "-server", "-Xms256m", "-Xmx1024m", "-XX:+UseG1GC" ]
}

test {
    useJUnitPlatform()
}

apply plugin: 'nebula.ospackage'
ospackage {
    packageName = 'hmci'
    release = '1'
    user = 'root'
    packager = "Mark Nellemann <mark.nellemann@gmail.com>"

    into '/opt/hmci'

    from(shadowJar.outputs.files) {
        into 'lib'
    }

    from('build/scriptsShadow') {
        into 'bin'
    }

    from('doc/') {
        into 'doc'
    }

    from(['README.md', 'LICENSE']) {
        into 'doc'
    }

}

buildRpm {
    dependsOn startShadowScripts
    os = "LINUX"
}

buildDeb {
    dependsOn startShadowScripts
}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    group = "verification"
    reports {
        xml.required = false
        csv.required = false
        html.destination file("${buildDir}/reports/coverage")
    }
}
test.finalizedBy jacocoTestReport

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.5   // TODO: Raise when more tests are implemented
            }
        }
    }
}
check.dependsOn jacocoTestCoverageVerification

jar {
    manifest {
        attributes(
            'Created-By'     : "Gradle ${gradle.gradleVersion}",
            'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}",
            'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
            'Build-User'     : System.properties['user.name'],
            'Build-Version'  : versioning.info.tag ?: (versioning.info.branch + "-" + versioning.info.build),
            'Build-Revision' : versioning.info.commit,
            'Build-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ss.SSSZ").toString(),
            'Add-Opens'      : 'java.base/java.lang.invoke' // To ignore "Illegal reflective access by retrofit2.Platform" warnings
        )
    }
}

tasks.create("packages") {
    group "build"
    dependsOn ":build"
    dependsOn ":buildDeb"
    dependsOn ":buildRpm"
}
