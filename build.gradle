import org.redline_rpm.header.Os

plugins {
    id 'java'
    id 'jacoco'
    id 'groovy'
    id 'application'
    id 'com.palantir.git-version' version "3.1.0"
    id "com.netflix.nebula.ospackage" version "11.8.1"
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

repositories {
    mavenCentral()
    mavenLocal()
}

group = projectGroup
version = projectVersion

dependencies {
    annotationProcessor 'info.picocli:picocli-codegen:4.7.6'
    implementation 'info.picocli:picocli:4.7.6'
    implementation 'org.slf4j:slf4j-api:2.0.12'
    implementation 'org.slf4j:slf4j-simple:2.0.13'
    implementation 'com.squareup.okhttp3:okhttp:4.11.0' // Also used by InfluxDB Client
    implementation 'com.influxdb:influxdb-client-java:6.12.0'   // Latest version supporting Java 8
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.4'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.15.4'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-toml:2.15.4'

    implementation 'io.prometheus:prometheus-metrics-core:1.0.0'
    implementation 'io.prometheus:prometheus-metrics-instrumentation-jvm:1.0.0'
    implementation 'io.prometheus:prometheus-metrics-exporter-httpserver:1.0.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
    testImplementation 'org.mock-server:mockserver-netty-no-dependencies:5.15.0'
}

application {
    mainClass.set('biz.nellemann.hmci.Application')
    applicationDefaultJvmArgs = [ "-server", "-Xms64m", "-Xmx256m", "-XX:+UseG1GC", "-XX:+ExitOnOutOfMemoryError", "-XX:+AlwaysPreTouch" ]
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

test {
    useJUnitPlatform()
}

run {
    args = ["--conf=doc/hmci.toml", "--debug"]
}

ospackage {
    packageName = 'hmci'
    release = '1'
    user = 'root'
    packager = "Mark Nellemann <mark.nellemann@gmail.com>"

    into '/opt/hmci'

    from(shadowJar.outputs.files) {
        into 'lib'
    }

    from('build/scriptsShadow') {
        into 'bin'
    }

    from('doc/') {
        into 'doc'
    }

    from(['README.md', 'LICENSE']) {
        into 'doc'
    }
}

buildDeb {
    dependsOn startShadowScripts
    installUtils file('scripts/utils.sh') as File
    installUtils file('scripts/config.sh') as File
    preUninstall file('scripts/deb-pre-rm.sh') as File
    postInstall file('scripts/deb-post-inst.sh') as File
}

buildRpm {
    dependsOn startShadowScripts
    installUtils file('scripts/utils.sh') as File
    installUtils file('scripts/config.sh') as File
    preUninstall file('scripts/rpm-pre-rm.sh') as File
    postTrans file('scripts/rpm-post-inst.sh') as File
    os Os.LINUX
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    group = "verification"
    reports {
        xml.required = false
        csv.required = false
        html.destination file("${buildDir}/reports/coverage")
    }
}
test.finalizedBy jacocoTestReport

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.4
            }
        }
    }
}
check.dependsOn jacocoTestCoverageVerification

jar {
    manifest {
        attributes(
            'Created-By'     : "Gradle ${gradle.gradleVersion}",
            'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}",
            'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
            'Build-User'     : System.properties['user.name'],
            'Build-Version'  : gitVersion(),
            'Build-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ss.SSSZ").toString(),
            'Add-Opens'      : 'java.base/java.lang.invoke' // To ignore "Illegal reflective access by retrofit2.Platform" warnings
        )
    }
}

tasks.register("packages") {
    group "build"
    dependsOn ":build"
    dependsOn ":buildDeb"
    dependsOn ":buildRpm"
}
